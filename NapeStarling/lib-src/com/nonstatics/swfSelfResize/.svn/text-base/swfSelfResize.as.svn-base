/**
 * @author: Sebastian Martens; sebastian@sebastian-martens.de
 * @copyright: Creative Commons. Free to use "as is"
 * @svn: $Id$
 */ 
package com.nonstatics.swfSelfResize{
	
	import flash.display.Sprite;
	import flash.external.ExternalInterface;
	
	/**
	 * handles the resizing of the flash container
	 * @author Sebastian Martens ( htpp://blog.sebastian-martens.de/ )
	 */
	public class swfSelfResize extends Sprite{
		
		// this JavaScript code will be injected first into the page
		// developement will be done in swfSelfResize.js file -> compressed -> pasted here
		// ### will be replaced by the swfId
		private const JS_CODE:String = "function swfSelfResizeGetNode(){var a='###';if(window[a]){return window[a]}else{return document[a]}return null}function swfSelfResizeResize(a,b){var c=swfSelfResizeGetNode();if(c){c.style.height=a+'px';c.style.width=b+'px'}}function swfSelfResizeAddHeight(a){swfSelfResizeResize((swfSelfResizeGetHeight()+a),swfSelfResizeGetWidth())}function swfSelfResizeAddWidth(a){swfSelfResizeResize(swfSelfResizeGetHeight(),(swfSelfResizeGetWidth()+a))}function swfSelfResizeGetHeight(){var b=swfSelfResizeGetNode();var a=parseInt(b.style.height,10);if(!a){return parseInt(b.getAttribute('height'),10)}else{return a}}function swfSelfResizeGetWidth(){var b=swfSelfResizeGetNode();var a=parseInt(b.style.height,10);if(!a){return parseInt(b.getAttribute('width'),10)}else{return a}};";
		private const JS_PATTERN:RegExp = /###/g;
		
		// internal id to identify match
		private var swfId:String;
		
		/**
		 * constructor
		 */ 
		public function swfSelfResize(){
			if( this.available() ){
				this.swfId = ExternalInterface.objectID; 
				
				// define callback handler
				ExternalInterface.addCallback("getSWFId",this.getSWFId);
				
				// injekt js code
				ExternalInterface.call("eval", this.getJSCode() );
			}
		}
		
		/**
		 * replaces a strings by pattern within the javascript code snipped
		 * @return replaced javascript code
		 */		
		private function getJSCode():String{
			return this.JS_CODE.replace( this.JS_PATTERN, this.swfId );
		}
		
		/**
		 * @private
		 *  
		 * returns the internal swfid
		 * @return internal swfid
		 */
		public function getSWFId():String{
			return this.swfId;
		}
		 
		/**
		 * returns if this class is available, because ExternalInterface is equired 
		 * @return returns if this class is available
		 */
		public function available():Boolean{
			return ExternalInterface.available;
		}
		
		/**
		 * resizes the flash container to needed dimensions 
		 * @param height - new needed height 
		 * @param width - new needed with
		 */
		public function resize( height:int, width:int ):void{
			if( this.available() ){
				ExternalInterface.call("swfSelfResizeResize",height,width);
			}
		}
		
		/**
		 * adds a specific value (+/-) to current height
		 * @param valueHeight
		 */
		public function addHeight( valueHeight:int ):void{
			if( this.available() ){
				ExternalInterface.call("swfSelfResizeAddHeight",valueHeight);
			}	
		}
		
		/**
		 * adds a specific value (+/-) to current width
		 * @param valueHeight
		 */
		public function addWidth( valueWidth:int ):void{
			if( this.available() ){
				ExternalInterface.call("swfSelfResizeAddWidth",valueWidth);
			}
		}
		
		/**
		 * returns the current height of the flash container 
		 * @return current height of the flash container 
		 */		
		public function getCurrentHeight():int{
			if( this.available() ){
				return ExternalInterface.call("swfSelfResizeGetHeight");
			}else return -1;
		}	
		
		/**
		 * returns the current width of the flash container 
		 * @return current width of the flash container 
		 */	
		public function getCurrentWidth():int{
			if( this.available() ){
				return ExternalInterface.call("swfSelfResizeGetWidth");
			}else return -1;
		}
		
	}
}